---
subcategory: "Kinesis Firehose"
layout: "aws"
page_title: "AWS: aws_kinesis_firehose_delivery_stream"
description: |-
  Provides a AWS Kinesis Firehose Delivery Stream
---

# Resource: aws_kinesis_firehose_delivery_stream

Provides a Kinesis Firehose Delivery Stream resource. Amazon Kinesis Firehose is a fully managed, elastic service to easily deliver real-time data streams to destinations such as Amazon S3 and Amazon Redshift.

For more details, see the [Amazon Kinesis Firehose Documentation][1].

## Example Usage

### Extended S3 Destination

```typescript
import * as constructs from "constructs";
import * as cdktf from "cdktf";
/*Provider bindings are generated by running cdktf get.
See https://cdk.tf/provider-generation for more details.*/
import * as aws from "./.gen/providers/aws";
class MyConvertedCode extends cdktf.TerraformStack {
  constructor(scope: constructs.Construct, name: string) {
    super(scope, name);
    const awsS3BucketBucket = new aws.s3Bucket.S3Bucket(this, "bucket", {
      bucket: "tf-test-bucket",
    });
    new aws.s3BucketAcl.S3BucketAcl(this, "bucket_acl", {
      acl: "private",
      bucket: cdktf.Token.asString(awsS3BucketBucket.id),
    });
    const dataAwsIamPolicyDocumentFirehoseAssumeRole =
      new aws.dataAwsIamPolicyDocument.DataAwsIamPolicyDocument(
        this,
        "firehose_assume_role",
        {
          statement: [
            {
              actions: ["sts:AssumeRole"],
              effect: "Allow",
              principals: [
                {
                  identifiers: ["firehose.amazonaws.com"],
                  type: "Service",
                },
              ],
            },
          ],
        }
      );
    const dataAwsIamPolicyDocumentLambdaAssumeRole =
      new aws.dataAwsIamPolicyDocument.DataAwsIamPolicyDocument(
        this,
        "lambda_assume_role",
        {
          statement: [
            {
              actions: ["sts:AssumeRole"],
              effect: "Allow",
              principals: [
                {
                  identifiers: ["lambda.amazonaws.com"],
                  type: "Service",
                },
              ],
            },
          ],
        }
      );
    const awsIamRoleFirehoseRole = new aws.iamRole.IamRole(
      this,
      "firehose_role",
      {
        assumeRolePolicy: cdktf.Token.asString(
          dataAwsIamPolicyDocumentFirehoseAssumeRole.json
        ),
        name: "firehose_test_role",
      }
    );
    const awsIamRoleLambdaIam = new aws.iamRole.IamRole(this, "lambda_iam", {
      assumeRolePolicy: cdktf.Token.asString(
        dataAwsIamPolicyDocumentLambdaAssumeRole.json
      ),
      name: "lambda_iam",
    });
    const awsLambdaFunctionLambdaProcessor =
      new aws.lambdaFunction.LambdaFunction(this, "lambda_processor", {
        filename: "lambda.zip",
        functionName: "firehose_lambda_processor",
        handler: "exports.handler",
        role: cdktf.Token.asString(awsIamRoleLambdaIam.arn),
        runtime: "nodejs16.x",
      });
    new aws.kinesisFirehoseDeliveryStream.KinesisFirehoseDeliveryStream(
      this,
      "extended_s3_stream",
      {
        destination: "extended_s3",
        extendedS3Configuration: {
          bucketArn: cdktf.Token.asString(awsS3BucketBucket.arn),
          processingConfiguration: {
            enabled: cdktf.Token.asBoolean("true"),
            processors: [
              {
                parameters: [
                  {
                    parameterName: "LambdaArn",
                    parameterValue:
                      "${" + awsLambdaFunctionLambdaProcessor.arn + "}:$LATEST",
                  },
                ],
                type: "Lambda",
              },
            ],
          },
          roleArn: cdktf.Token.asString(awsIamRoleFirehoseRole.arn),
        },
        name: "terraform-kinesis-firehose-extended-s3-test-stream",
      }
    );
  }
}

```

### Extended S3 Destination with dynamic partitioning

These examples use built-in Firehose functionality, rather than requiring a lambda.

```terraform
resource "aws_kinesis_firehose_delivery_stream" "extended_s3_stream" {
  name        = "terraform-kinesis-firehose-extended-s3-test-stream"
  destination = "extended_s3"

  extended_s3_configuration {
    role_arn   = aws_iam_role.firehose_role.arn
    bucket_arn = aws_s3_bucket.bucket.arn

    buffering_size = 64

    # https://docs.aws.amazon.com/firehose/latest/dev/dynamic-partitioning.html
    dynamic_partitioning_configuration {
      enabled = "true"
    }

    # Example prefix using partitionKeyFromQuery, applicable to JQ processor
    prefix              = "data/customer_id=!{partitionKeyFromQuery:customer_id}/year=!{timestamp:yyyy}/month=!{timestamp:MM}/day=!{timestamp:dd}/hour=!{timestamp:HH}/"
    error_output_prefix = "errors/year=!{timestamp:yyyy}/month=!{timestamp:MM}/day=!{timestamp:dd}/hour=!{timestamp:HH}/!{firehose:error-output-type}/"

    processing_configuration {
      enabled = "true"

      # Multi-record deaggregation processor example
      processors {
        type = "RecordDeAggregation"
        parameters {
          parameter_name  = "SubRecordType"
          parameter_value = "JSON"
        }
      }

      # New line delimiter processor example
      processors {
        type = "AppendDelimiterToRecord"
      }

      # JQ processor example
      processors {
        type = "MetadataExtraction"
        parameters {
          parameter_name  = "JsonParsingEngine"
          parameter_value = "JQ-1.6"
        }
        parameters {
          parameter_name  = "MetadataExtractionQuery"
          parameter_value = "{customer_id:.customer_id}"
        }
      }
    }
  }
}
```

### Redshift Destination

```terraform
resource "aws_redshift_cluster" "test_cluster" {
  cluster_identifier = "tf-redshift-cluster"
  database_name      = "test"
  master_username    = "testuser"
  master_password    = "T3stPass"
  node_type          = "dc1.large"
  cluster_type       = "single-node"
}

resource "aws_kinesis_firehose_delivery_stream" "test_stream" {
  name        = "terraform-kinesis-firehose-test-stream"
  destination = "redshift"

  redshift_configuration {
    role_arn           = aws_iam_role.firehose_role.arn
    cluster_jdbcurl    = "jdbc:redshift://${aws_redshift_cluster.test_cluster.endpoint}/${aws_redshift_cluster.test_cluster.database_name}"
    username           = "testuser"
    password           = "T3stPass"
    data_table_name    = "test-table"
    copy_options       = "delimiter '|'" # the default delimiter
    data_table_columns = "test-col"
    s3_backup_mode     = "Enabled"

    s3_configuration {
      role_arn           = aws_iam_role.firehose_role.arn
      bucket_arn         = aws_s3_bucket.bucket.arn
      buffering_size     = 10
      buffering_interval = 400
      compression_format = "GZIP"
    }

    s3_backup_configuration {
      role_arn           = aws_iam_role.firehose_role.arn
      bucket_arn         = aws_s3_bucket.bucket.arn
      buffering_size     = 15
      buffering_interval = 300
      compression_format = "GZIP"
    }
  }
}
```

### Elasticsearch Destination

```terraform
resource "aws_elasticsearch_domain" "test_cluster" {
  domain_name = "firehose-es-test"
}

resource "aws_kinesis_firehose_delivery_stream" "test_stream" {
  name        = "terraform-kinesis-firehose-test-stream"
  destination = "elasticsearch"

  elasticsearch_configuration {
    domain_arn = aws_elasticsearch_domain.test_cluster.arn
    role_arn   = aws_iam_role.firehose_role.arn
    index_name = "test"
    type_name  = "test"

    s3_configuration {
      role_arn           = aws_iam_role.firehose_role.arn
      bucket_arn         = aws_s3_bucket.bucket.arn
      buffering_size     = 10
      buffering_interval = 400
      compression_format = "GZIP"
    }

    processing_configuration {
      enabled = "true"

      processors {
        type = "Lambda"

        parameters {
          parameter_name  = "LambdaArn"
          parameter_value = "${aws_lambda_function.lambda_processor.arn}:$LATEST"
        }
      }
    }
  }
}
```

### Elasticsearch Destination With VPC

```terraform
resource "aws_elasticsearch_domain" "test_cluster" {
  domain_name = "es-test"

  cluster_config {
    instance_count         = 2
    zone_awareness_enabled = true
    instance_type          = "t2.small.elasticsearch"
  }

  ebs_options {
    ebs_enabled = true
    volume_size = 10
  }

  vpc_options {
    security_group_ids = [aws_security_group.first.id]
    subnet_ids         = [aws_subnet.first.id, aws_subnet.second.id]
  }
}

data "aws_iam_policy_document" "firehose-elasticsearch" {
  statement {
    effect  = "Allow"
    actions = ["es:*"]

    resources = [
      aws_elasticsearch_domain.test_cluster.arn,
      "${aws_elasticsearch_domain.test_cluster.arn}/*",
    ]
  }

  statement {
    effect = "Allow"

    actions = [
      "ec2:DescribeVpcs",
      "ec2:DescribeVpcAttribute",
      "ec2:DescribeSubnets",
      "ec2:DescribeSecurityGroups",
      "ec2:DescribeNetworkInterfaces",
      "ec2:CreateNetworkInterface",
      "ec2:CreateNetworkInterfacePermission",
      "ec2:DeleteNetworkInterface",
    ]

    resources = ["*"]
  }
}

resource "aws_iam_role_policy" "firehose-elasticsearch" {
  name   = "elasticsearch"
  role   = aws_iam_role.firehose.id
  policy = data.aws_iam_policy_document.firehose-elasticsearch.json
}

resource "aws_kinesis_firehose_delivery_stream" "test" {
  depends_on = [aws_iam_role_policy.firehose-elasticsearch]

  name        = "terraform-kinesis-firehose-es"
  destination = "elasticsearch"

  elasticsearch_configuration {
    domain_arn = aws_elasticsearch_domain.test_cluster.arn
    role_arn   = aws_iam_role.firehose.arn
    index_name = "test"
    type_name  = "test"

    s3_configuration {
      role_arn   = aws_iam_role.firehose.arn
      bucket_arn = aws_s3_bucket.bucket.arn
    }

    vpc_config {
      subnet_ids         = [aws_subnet.first.id, aws_subnet.second.id]
      security_group_ids = [aws_security_group.first.id]
      role_arn           = aws_iam_role.firehose.arn
    }
  }
}
```

### Opensearch Destination

```terraform
resource "aws_opensearch_domain" "test_cluster" {
  domain_name = "firehose-os-test"
}

resource "aws_kinesis_firehose_delivery_stream" "test_stream" {
  name        = "terraform-kinesis-firehose-test-stream"
  destination = "opensearch"

  opensearch_configuration {
    domain_arn = aws_opensearch_domain.test_cluster.arn
    role_arn   = aws_iam_role.firehose_role.arn
    index_name = "test"

    s3_configuration {
      role_arn           = aws_iam_role.firehose_role.arn
      bucket_arn         = aws_s3_bucket.bucket.arn
      buffering_size     = 10
      buffering_interval = 400
      compression_format = "GZIP"
    }

    processing_configuration {
      enabled = "true"

      processors {
        type = "Lambda"

        parameters {
          parameter_name  = "LambdaArn"
          parameter_value = "${aws_lambda_function.lambda_processor.arn}:$LATEST"
        }
      }
    }
  }
}
```

### Opensearch Destination With VPC

```terraform
resource "aws_opensearch_domain" "test_cluster" {
  domain_name = "es-test"

  cluster_config {
    instance_count         = 2
    zone_awareness_enabled = true
    instance_type          = "m4.large.search"
  }

  ebs_options {
    ebs_enabled = true
    volume_size = 10
  }

  vpc_options {
    security_group_ids = [aws_security_group.first.id]
    subnet_ids         = [aws_subnet.first.id, aws_subnet.second.id]
  }
}

resource "aws_iam_role_policy" "firehose-opensearch" {
  name   = "opensearch"
  role   = aws_iam_role.firehose.id
  policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": [
        "es:*"
      ],
      "Resource": [
        "${aws_opensearch_domain.test_cluster.arn}",
        "${aws_opensearch_domain.test_cluster.arn}/*"
      ]
        },
        {
          "Effect": "Allow",
          "Action": [
            "ec2:DescribeVpcs",
            "ec2:DescribeVpcAttribute",
            "ec2:DescribeSubnets",
            "ec2:DescribeSecurityGroups",
            "ec2:DescribeNetworkInterfaces",
            "ec2:CreateNetworkInterface",
            "ec2:CreateNetworkInterfacePermission",
            "ec2:DeleteNetworkInterface"
          ],
          "Resource": [
            "*"
          ]
        }
  ]
}
EOF
}

resource "aws_kinesis_firehose_delivery_stream" "test" {
  depends_on = [aws_iam_role_policy.firehose-opensearch]

  name        = "terraform-kinesis-firehose-os"
  destination = "opensearch"

  opensearch_configuration {
    domain_arn = aws_opensearch_domain.test_cluster.arn
    role_arn   = aws_iam_role.firehose.arn
    index_name = "test"

    s3_configuration {
      role_arn   = aws_iam_role.firehose.arn
      bucket_arn = aws_s3_bucket.bucket.arn
    }

    vpc_config {
      subnet_ids         = [aws_subnet.first.id, aws_subnet.second.id]
      security_group_ids = [aws_security_group.first.id]
      role_arn           = aws_iam_role.firehose.arn
    }
  }
}
```

### Splunk Destination

```terraform
resource "aws_kinesis_firehose_delivery_stream" "test_stream" {
  name        = "terraform-kinesis-firehose-test-stream"
  destination = "splunk"

  splunk_configuration {
    hec_endpoint               = "https://http-inputs-mydomain.splunkcloud.com:443"
    hec_token                  = "51D4DA16-C61B-4F5F-8EC7-ED4301342A4A"
    hec_acknowledgment_timeout = 600
    hec_endpoint_type          = "Event"
    s3_backup_mode             = "FailedEventsOnly"

    s3_configuration {
      role_arn           = aws_iam_role.firehose.arn
      bucket_arn         = aws_s3_bucket.bucket.arn
      buffering_size     = 10
      buffering_interval = 400
      compression_format = "GZIP"
    }
  }
}
```

### HTTP Endpoint (e.g., New Relic) Destination

```terraform
resource "aws_kinesis_firehose_delivery_stream" "test_stream" {
  name        = "terraform-kinesis-firehose-test-stream"
  destination = "http_endpoint"

  http_endpoint_configuration {
    url                = "https://aws-api.newrelic.com/firehose/v1"
    name               = "New Relic"
    access_key         = "my-key"
    buffering_size     = 15
    buffering_interval = 600
    role_arn           = aws_iam_role.firehose.arn
    s3_backup_mode     = "FailedDataOnly"

    s3_configuration {
      role_arn           = aws_iam_role.firehose.arn
      bucket_arn         = aws_s3_bucket.bucket.arn
      buffering_size     = 10
      buffering_interval = 400
      compression_format = "GZIP"
    }

    request_configuration {
      content_encoding = "GZIP"

      common_attributes {
        name  = "testname"
        value = "testvalue"
      }

      common_attributes {
        name  = "testname2"
        value = "testvalue2"
      }
    }
  }
}
```

## Argument Reference

The following arguments are supported:

* `name` - (Required) A name to identify the stream. This is unique to the AWS account and region the Stream is created in. When using for WAF logging, name must be prefixed with `awsWafLogs`. See [AWS Documentation](https://docs.aws.amazon.com/waf/latest/developerguide/waf-policies.html#waf-policies-logging-config) for more details.
* `tags` - (Optional) A map of tags to assign to the resource. If configured with a provider [`defaultTags` configuration block](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#default_tags-configuration-block) present, tags with matching keys will overwrite those defined at the provider-level.
* `kinesisSourceConfiguration` - (Optional) Allows the ability to specify the kinesis stream that is used as the source of the firehose delivery stream.
* `serverSideEncryption` - (Optional) Encrypt at rest options.
Server-side encryption should not be enabled when a kinesis stream is configured as the source of the firehose delivery stream.
* `destination` â€“ (Required) This is the destination to where the data is delivered. The only options are `s3` (Deprecated, use `extendedS3` instead), `extendedS3`, `redshift`, `elasticsearch`, `splunk`, `httpEndpoint` and `opensearch`.
is redshift). More details are given below.
* `extendedS3Configuration` - (Optional, only Required when `destination` is `extendedS3`) Enhanced configuration options for the s3 destination. More details are given below.
* `redshiftConfiguration` - (Optional) Configuration options if redshift is the destination.
Using `redshiftConfiguration` requires the user to also specify a
`s3Configuration` block. More details are given below.
* `elasticsearchConfiguration` - (Optional) Configuration options if elasticsearch is the destination. More details are given below.
* `opensearchConfiguration` - (Optional) Configuration options if opensearch is the destination. More details are given below.
* `splunkConfiguration` - (Optional) Configuration options if splunk is the destination. More details are given below.
* `httpEndpointConfiguration` - (Optional) Configuration options if http_endpoint is the destination. requires the user to also specify a `s3Configuration` block.  More details are given below.

The `kinesisSourceConfiguration` object supports the following:

* `kinesisStreamArn` (Required) The kinesis stream used as the source of the firehose delivery stream.
* `roleArn` (Required) The ARN of the role that provides access to the source Kinesis stream.

The `serverSideEncryption` object supports the following:

* `enabled` - (Optional) Whether to enable encryption at rest. Default is `false`.
* `keyType`- (Optional) Type of encryption key. Default is `awsOwnedCmk`. Valid values are `awsOwnedCmk` and `customerManagedCmk`
* `keyArn` - (Optional) Amazon Resource Name (ARN) of the encryption key. Required when `keyType` is `customerManagedCmk`.

The `extendedS3Configuration` object supports the same fields from [s3_configuration](#s3-configuration) as well as the following:

* `dataFormatConversionConfiguration` - (Optional) Nested argument for the serializer, deserializer, and schema for converting data from the JSON format to the Parquet or ORC format before writing it to Amazon S3. More details given below.
* `processingConfiguration` - (Optional) The data processing configuration.  More details are given below.
* `s3BackupMode` - (Optional) The Amazon S3 backup mode.  Valid values are `disabled` and `enabled`.  Default value is `disabled`.
* `s3BackupConfiguration` - (Optional) The configuration for backup in Amazon S3. Required if `s3BackupMode` is `enabled`. Supports the same fields as `s3Configuration` object.
* `dynamicPartitioningConfiguration` - (Optional) The configuration for dynamic partitioning. See [Dynamic Partitioning Configuration](#dynamic_partitioning_configuration) below for more details. Required when using dynamic partitioning.

The `redshiftConfiguration` object supports the following:

* `clusterJdbcurl` - (Required) The jdbcurl of the redshift cluster.
* `username` - (Required) The username that the firehose delivery stream will assume. It is strongly recommended that the username and password provided is used exclusively for Amazon Kinesis Firehose purposes, and that the permissions for the account are restricted for Amazon Redshift INSERT permissions.
* `password` - (Required) The password for the username above.
* `retryDuration` - (Optional) The length of time during which Firehose retries delivery after a failure, starting from the initial request and including the first attempt. The default value is 3600 seconds (60 minutes). Firehose does not retry if the value of DurationInSeconds is 0 (zero) or if the first delivery attempt takes longer than the current value.
* `roleArn` - (Required) The arn of the role the stream assumes.
* `s3Configuration` - (Required) The S3 Configuration. See [s3_configuration](#s3-configuration) for more details.
* `s3BackupMode` - (Optional) The Amazon S3 backup mode.  Valid values are `disabled` and `enabled`.  Default value is `disabled`.
* `s3BackupConfiguration` - (Optional) The configuration for backup in Amazon S3. Required if `s3BackupMode` is `enabled`. Supports the same fields as `s3Configuration` object.
* `dataTableName` - (Required) The name of the table in the redshift cluster that the s3 bucket will copy to.
* `copyOptions` - (Optional) Copy options for copying the data from the s3 intermediate bucket into redshift, for example to change the default delimiter. For valid values, see the [AWS documentation](http://docs.aws.amazon.com/firehose/latest/APIReference/API_CopyCommand.html)
* `dataTableColumns` - (Optional) The data table columns that will be targeted by the copy command.
* `cloudwatchLoggingOptions` - (Optional) The CloudWatch Logging Options for the delivery stream. More details are given below
* `processingConfiguration` - (Optional) The data processing configuration.  More details are given below.

The `elasticsearchConfiguration` object supports the following:

* `bufferingInterval` - (Optional) Buffer incoming data for the specified period of time, in seconds between 60 to 900, before delivering it to the destination.  The default value is 300s.
* `bufferingSize` - (Optional) Buffer incoming data to the specified size, in MBs between 1 to 100, before delivering it to the destination.  The default value is 5MB.
* `domainArn` - (Optional) The ARN of the Amazon ES domain.  The pattern needs to be `arn:.*`.  Conflicts with `clusterEndpoint`.
* `clusterEndpoint` - (Optional) The endpoint to use when communicating with the cluster. Conflicts with `domainArn`.
* `indexName` - (Required) The Elasticsearch index name.
* `indexRotationPeriod` - (Optional) The Elasticsearch index rotation period.  Index rotation appends a timestamp to the IndexName to facilitate expiration of old data.  Valid values are `noRotation`, `oneHour`, `oneDay`, `oneWeek`, and `oneMonth`.  The default value is `oneDay`.
* `retryDuration` - (Optional) After an initial failure to deliver to Amazon Elasticsearch, the total amount of time, in seconds between 0 to 7200, during which Firehose re-attempts delivery (including the first attempt).  After this time has elapsed, the failed documents are written to Amazon S3.  The default value is 300s.  There will be no retry if the value is 0.
* `roleArn` - (Required) The ARN of the IAM role to be assumed by Firehose for calling the Amazon ES Configuration API and for indexing documents.  The IAM role must have permission for `describeElasticsearchDomain`, `describeElasticsearchDomains`, and `describeElasticsearchDomainConfig`.  The pattern needs to be `arn:.*`.
* `s3Configuration` - (Required) The S3 Configuration. See [s3_configuration](#s3-configuration) for more details.
* `s3BackupMode` - (Optional) Defines how documents should be delivered to Amazon S3.  Valid values are `failedDocumentsOnly` and `allDocuments`.  Default value is `failedDocumentsOnly`.
* `typeName` - (Optional) The Elasticsearch type name with maximum length of 100 characters.
* `cloudwatchLoggingOptions` - (Optional) The CloudWatch Logging Options for the delivery stream. More details are given below
* `vpcConfig` - (Optional) The VPC configuration for the delivery stream to connect to Elastic Search associated with the VPC. More details are given below
* `processingConfiguration` - (Optional) The data processing configuration.  More details are given below.

The `opensearchConfiguration` object supports the following:

* `bufferingInterval` - (Optional) Buffer incoming data for the specified period of time, in seconds between 60 to 900, before delivering it to the destination.  The default value is 300s.
* `bufferingSize` - (Optional) Buffer incoming data to the specified size, in MBs between 1 to 100, before delivering it to the destination.  The default value is 5MB.
* `domainArn` - (Optional) The ARN of the Amazon ES domain.  The pattern needs to be `arn:.*`.  Conflicts with `clusterEndpoint`.
* `clusterEndpoint` - (Optional) The endpoint to use when communicating with the cluster. Conflicts with `domainArn`.
* `indexName` - (Required) The Opensearch index name.
* `indexRotationPeriod` - (Optional) The Opensearch index rotation period.  Index rotation appends a timestamp to the IndexName to facilitate expiration of old data.  Valid values are `noRotation`, `oneHour`, `oneDay`, `oneWeek`, and `oneMonth`.  The default value is `oneDay`.
* `retryDuration` - (Optional) After an initial failure to deliver to Amazon OpenSearch, the total amount of time, in seconds between 0 to 7200, during which Firehose re-attempts delivery (including the first attempt).  After this time has elapsed, the failed documents are written to Amazon S3.  The default value is 300s.  There will be no retry if the value is 0.
* `roleArn` - (Required) The ARN of the IAM role to be assumed by Firehose for calling the Amazon ES Configuration API and for indexing documents.  The IAM role must have permission for `describeDomain`, `describeDomains`, and `describeDomainConfig`.  The pattern needs to be `arn:.*`.
* `s3Configuration` - (Required) The S3 Configuration. See [s3_configuration](#s3-configuration) for more details.
* `s3BackupMode` - (Optional) Defines how documents should be delivered to Amazon S3.  Valid values are `failedDocumentsOnly` and `allDocuments`.  Default value is `failedDocumentsOnly`.
* `typeName` - (Optional) The Elasticsearch type name with maximum length of 100 characters. Types are deprecated in OpenSearch_1.1. TypeName must be empty.
* `cloudwatchLoggingOptions` - (Optional) The CloudWatch Logging Options for the delivery stream. More details are given below
* `vpcConfig` - (Optional) The VPC configuration for the delivery stream to connect to OpenSearch associated with the VPC. More details are given below
* `processingConfiguration` - (Optional) The data processing configuration.  More details are given below.

The `splunkConfiguration` objects supports the following:

* `hecAcknowledgmentTimeout` - (Optional) The amount of time, in seconds between 180 and 600, that Kinesis Firehose waits to receive an acknowledgment from Splunk after it sends it data.
* `hecEndpoint` - (Required) The HTTP Event Collector (HEC) endpoint to which Kinesis Firehose sends your data.
* `hecEndpointType` - (Optional) The HEC endpoint type. Valid values are `raw` or `event`. The default value is `raw`.
* `hecToken` - (Required) The GUID that you obtain from your Splunk cluster when you create a new HEC endpoint.
* `s3Configuration` - (Required) The S3 Configuration. See [s3_configuration](#s3-configuration) for more details.
* `s3BackupMode` - (Optional) Defines how documents should be delivered to Amazon S3.  Valid values are `failedEventsOnly` and `allEvents`.  Default value is `failedEventsOnly`.
* `retryDuration` - (Optional) After an initial failure to deliver to Splunk, the total amount of time, in seconds between 0 to 7200, during which Firehose re-attempts delivery (including the first attempt).  After this time has elapsed, the failed documents are written to Amazon S3.  The default value is 300s.  There will be no retry if the value is 0.
* `cloudwatchLoggingOptions` - (Optional) The CloudWatch Logging Options for the delivery stream. More details are given below.
* `processingConfiguration` - (Optional) The data processing configuration.  More details are given below.

The `httpEndpointConfiguration` objects supports the following:

* `url` - (Required) The HTTP endpoint URL to which Kinesis Firehose sends your data.
* `name` - (Optional) The HTTP endpoint name.
* `accessKey` - (Optional) The access key required for Kinesis Firehose to authenticate with the HTTP endpoint selected as the destination.
* `roleArn` - (Required) Kinesis Data Firehose uses this IAM role for all the permissions that the delivery stream needs. The pattern needs to be `arn:.*`.
* `s3Configuration` - (Required) The S3 Configuration. See [s3_configuration](#s3-configuration) for more details.
* `s3BackupMode` - (Optional) Defines how documents should be delivered to Amazon S3.  Valid values are `failedDataOnly` and `allData`.  Default value is `failedDataOnly`.
* `bufferingSize` - (Optional) Buffer incoming data to the specified size, in MBs, before delivering it to the destination. The default value is 5.
* `bufferingInterval` - (Optional) Buffer incoming data for the specified period of time, in seconds, before delivering it to the destination. The default value is 300 (5 minutes).
* `cloudwatchLoggingOptions` - (Optional) The CloudWatch Logging Options for the delivery stream. More details are given below.
* `processingConfiguration` - (Optional) The data processing configuration.  More details are given below.
* `requestConfiguration` - (Optional) The request configuration.  More details are given below.
* `retryDuration` - (Optional) Total amount of seconds Firehose spends on retries. This duration starts after the initial attempt fails, It does not include the time periods during which Firehose waits for acknowledgment from the specified destination after each attempt. Valid values between `0` and `7200`. Default is `300`.

The `cloudwatchLoggingOptions` object supports the following:

* `enabled` - (Optional) Enables or disables the logging. Defaults to `false`.
* `logGroupName` - (Optional) The CloudWatch group name for logging. This value is required if `enabled` is true.
* `logStreamName` - (Optional) The CloudWatch log stream name for logging. This value is required if `enabled` is true.

The `processingConfiguration` object supports the following:

* `enabled` - (Optional) Enables or disables data processing.
* `processors` - (Optional) Array of data processors. More details are given below

The `processors` array objects support the following:

* `type` - (Required) The type of processor. Valid Values: `recordDeAggregation`, `lambda`, `metadataExtraction`, `appendDelimiterToRecord`. Validation is done against [AWS SDK constants](https://docs.aws.amazon.com/sdk-for-go/api/service/firehose/#pkg-constants); so that values not explicitly listed may also work.
* `parameters` - (Optional) Array of processor parameters. More details are given below

The `parameters` array objects support the following:

* `parameterName` - (Required) Parameter name. Valid Values: `lambdaArn`, `numberOfRetries`, `metadataExtractionQuery`, `jsonParsingEngine`, `roleArn`, `bufferSizeInMBs`, `bufferIntervalInSeconds`, `subRecordType`, `delimiter`. Validation is done against [AWS SDK constants](https://docs.aws.amazon.com/sdk-for-go/api/service/firehose/#pkg-constants); so that values not explicitly listed may also work.
* `parameterValue` - (Required) Parameter value. Must be between 1 and 512 length (inclusive). When providing a Lambda ARN, you should specify the resource version as well.

~> **NOTE:** Parameters with default values, including `numberOfRetries`(default: 3), `roleArn`(default: firehose role ARN), `bufferSizeInMBs`(default: 3), and `bufferIntervalInSeconds`(default: 60), are not stored in terraform state. To prevent perpetual differences, it is therefore recommended to only include parameters with non-default values.

The `requestConfiguration` object supports the following:

* `contentEncoding` - (Optional) Kinesis Data Firehose uses the content encoding to compress the body of a request before sending the request to the destination. Valid values are `none` and `gzip`.  Default value is `none`.
* `commonAttributes` - (Optional) Describes the metadata sent to the HTTP endpoint destination. More details are given below

The `commonAttributes` array objects support the following:

* `name` - (Required) The name of the HTTP endpoint common attribute.
* `value` - (Optional) The value of the HTTP endpoint common attribute.

The `vpcConfig` object supports the following:

* `subnetIds` - (Required) A list of subnet IDs to associate with Kinesis Firehose.
* `securityGroupIds` - (Required) A list of security group IDs to associate with Kinesis Firehose.
* `roleArn` - (Required) The ARN of the IAM role to be assumed by Firehose for calling the Amazon EC2 configuration API and for creating network interfaces. Make sure role has necessary [IAM permissions](https://docs.aws.amazon.com/firehose/latest/dev/controlling-access.html#using-iam-es-vpc)

### data_format_conversion_configuration

~> **NOTE:** Once configured, the data format conversion configuration can only be disabled, in which the configuration values will remain, but will not be active. It is not currently possible to completely remove the configuration without recreating the resource.

Example:

```terraform
resource "aws_kinesis_firehose_delivery_stream" "example" {
  # ... other configuration ...
  extended_s3_configuration {
    # Must be at least 64
    buffering_size = 128

    # ... other configuration ...
    data_format_conversion_configuration {
      input_format_configuration {
        deserializer {
          hive_json_ser_de {}
        }
      }

      output_format_configuration {
        serializer {
          orc_ser_de {}
        }
      }

      schema_configuration {
        database_name = aws_glue_catalog_table.example.database_name
        role_arn      = aws_iam_role.example.arn
        table_name    = aws_glue_catalog_table.example.name
      }
    }
  }
}
```

* `inputFormatConfiguration` - (Required) Nested argument that specifies the deserializer that you want Kinesis Data Firehose to use to convert the format of your data from JSON. More details below.
* `outputFormatConfiguration` - (Required) Nested argument that specifies the serializer that you want Kinesis Data Firehose to use to convert the format of your data to the Parquet or ORC format. More details below.
* `schemaConfiguration` - (Required) Nested argument that specifies the AWS Glue Data Catalog table that contains the column information. More details below.
* `enabled` - (Optional) Defaults to `true`. Set it to `false` if you want to disable format conversion while preserving the configuration details.

#### S3 Configuration

* `roleArn` - (Required) The ARN of the AWS credentials.
* `bucketArn` - (Required) The ARN of the S3 bucket
* `prefix` - (Optional) The "YYYY/MM/DD/HH" time format prefix is automatically used for delivered S3 files. You can specify an extra prefix to be added in front of the time format prefix. Note that if the prefix ends with a slash, it appears as a folder in the S3 bucket
* `bufferingSize` - (Optional) Buffer incoming data to the specified size, in MBs, before delivering it to the destination. The default value is 5.
  We recommend setting SizeInMBs to a value greater than the amount of data you typically ingest into the delivery stream in 10 seconds. For example, if you typically ingest data at 1 MB/sec set SizeInMBs to be 10 MB or higher.
* `bufferingInterval` - (Optional) Buffer incoming data for the specified period of time, in seconds, before delivering it to the destination. The default value is 300.
* `compressionFormat` - (Optional) The compression format. If no value is specified, the default is `uncompressed`. Other supported values are `gzip`, `zip`, `snappy`, & `hadoopSnappy`.
* `errorOutputPrefix` - (Optional) Prefix added to failed records before writing them to S3. Not currently supported for `redshift` destination. This prefix appears immediately following the bucket name. For information about how to specify this prefix, see [Custom Prefixes for Amazon S3 Objects](https://docs.aws.amazon.com/firehose/latest/dev/s3-prefixes.html).
* `kmsKeyArn` - (Optional) Specifies the KMS key ARN the stream will use to encrypt data. If not set, no encryption will
  be used.
* `cloudwatchLoggingOptions` - (Optional) The CloudWatch Logging Options for the delivery stream. More details are given below

#### input_format_configuration

* `deserializer` - (Required) Nested argument that specifies which deserializer to use. You can choose either the Apache Hive JSON SerDe or the OpenX JSON SerDe. More details below.

##### deserializer

~> **NOTE:** One of the deserializers must be configured. If no nested configuration needs to occur simply declare as `XXX_json_ser_de = []` or `XXX_json_ser_de {}`.

* `hiveJsonSerDe` - (Optional) Nested argument that specifies the native Hive / HCatalog JsonSerDe. More details below.
* `openXJsonSerDe` - (Optional) Nested argument that specifies the OpenX SerDe. More details below.

###### hive_json_ser_de

* `timestampFormats` - (Optional) A list of how you want Kinesis Data Firehose to parse the date and time stamps that may be present in your input data JSON. To specify these format strings, follow the pattern syntax of JodaTime's DateTimeFormat format strings. For more information, see [Class DateTimeFormat](https://www.joda.org/joda-time/apidocs/org/joda/time/format/DateTimeFormat.html). You can also use the special value millis to parse time stamps in epoch milliseconds. If you don't specify a format, Kinesis Data Firehose uses java.sql.Timestamp::valueOf by default.

###### open_x_json_ser_de

* `caseInsensitive` - (Optional) When set to true, which is the default, Kinesis Data Firehose converts JSON keys to lowercase before deserializing them.
* `columnToJsonKeyMappings` - (Optional) A map of column names to JSON keys that aren't identical to the column names. This is useful when the JSON contains keys that are Hive keywords. For example, timestamp is a Hive keyword. If you have a JSON key named timestamp, set this parameter to `{ ts = "timestamp" }` to map this key to a column named ts.
* `convertDotsInJsonKeysToUnderscores` - (Optional) When set to `true`, specifies that the names of the keys include dots and that you want Kinesis Data Firehose to replace them with underscores. This is useful because Apache Hive does not allow dots in column names. For example, if the JSON contains a key whose name is "a.b", you can define the column name to be "a_b" when using this option. Defaults to `false`.

#### output_format_configuration

* `serializer` - (Required) Nested argument that specifies which serializer to use. You can choose either the ORC SerDe or the Parquet SerDe. More details below.

##### serializer

~> **NOTE:** One of the serializers must be configured. If no nested configuration needs to occur simply declare as `XXX_ser_de = []` or `XXX_ser_de {}`.

* `orcSerDe` - (Optional) Nested argument that specifies converting data to the ORC format before storing it in Amazon S3. For more information, see [Apache ORC](https://orc.apache.org/docs/). More details below.
* `parquetSerDe` - (Optional) Nested argument that specifies converting data to the Parquet format before storing it in Amazon S3. For more information, see [Apache Parquet](https://parquet.apache.org/documentation/latest/). More details below.

###### orc_ser_de

* `blockSizeBytes` - (Optional) The Hadoop Distributed File System (HDFS) block size. This is useful if you intend to copy the data from Amazon S3 to HDFS before querying. The default is 256 MiB and the minimum is 64 MiB. Kinesis Data Firehose uses this value for padding calculations.
* `bloomFilterColumns` - (Optional) A list of column names for which you want Kinesis Data Firehose to create bloom filters.
* `bloomFilterFalsePositiveProbability` - (Optional) The Bloom filter false positive probability (FPP). The lower the FPP, the bigger the Bloom filter. The default value is `005`, the minimum is `0`, and the maximum is `1`.
* `compression` - (Optional) The compression code to use over data blocks. The default is `snappy`.
* `dictionaryKeyThreshold` - (Optional) A float that represents the fraction of the total number of non-null rows. To turn off dictionary encoding, set this fraction to a number that is less than the number of distinct keys in a dictionary. To always use dictionary encoding, set this threshold to `1`.
* `enablePadding` - (Optional) Set this to `true` to indicate that you want stripes to be padded to the HDFS block boundaries. This is useful if you intend to copy the data from Amazon S3 to HDFS before querying. The default is `false`.
* `formatVersion` - (Optional) The version of the file to write. The possible values are `v011` and `v012`. The default is `v012`.
* `paddingTolerance` - (Optional) A float between 0 and 1 that defines the tolerance for block padding as a decimal fraction of stripe size. The default value is `005`, which means 5 percent of stripe size. For the default values of 64 MiB ORC stripes and 256 MiB HDFS blocks, the default block padding tolerance of 5 percent reserves a maximum of 3.2 MiB for padding within the 256 MiB block. In such a case, if the available size within the block is more than 3.2 MiB, a new, smaller stripe is inserted to fit within that space. This ensures that no stripe crosses block boundaries and causes remote reads within a node-local task. Kinesis Data Firehose ignores this parameter when `enablePadding` is `false`.
* `rowIndexStride` - (Optional) The number of rows between index entries. The default is `10000` and the minimum is `1000`.
* `stripeSizeBytes` - (Optional) The number of bytes in each stripe. The default is 64 MiB and the minimum is 8 MiB.

###### parquet_ser_de

* `blockSizeBytes` - (Optional) The Hadoop Distributed File System (HDFS) block size. This is useful if you intend to copy the data from Amazon S3 to HDFS before querying. The default is 256 MiB and the minimum is 64 MiB. Kinesis Data Firehose uses this value for padding calculations.
* `compression` - (Optional) The compression code to use over data blocks. The possible values are `uncompressed`, `snappy`, and `gzip`, with the default being `snappy`. Use `snappy` for higher decompression speed. Use `gzip` if the compression ratio is more important than speed.
* `enableDictionaryCompression` - (Optional) Indicates whether to enable dictionary compression.
* `maxPaddingBytes` - (Optional) The maximum amount of padding to apply. This is useful if you intend to copy the data from Amazon S3 to HDFS before querying. The default is `0`.
* `pageSizeBytes` - (Optional) The Parquet page size. Column chunks are divided into pages. A page is conceptually an indivisible unit (in terms of compression and encoding). The minimum value is 64 KiB and the default is 1 MiB.
* `writerVersion` - (Optional) Indicates the version of row format to output. The possible values are `v1` and `v2`. The default is `v1`.

#### schema_configuration

* `databaseName` - (Required) Specifies the name of the AWS Glue database that contains the schema for the output data.
* `roleArn` - (Required) The role that Kinesis Data Firehose can use to access AWS Glue. This role must be in the same account you use for Kinesis Data Firehose. Cross-account roles aren't allowed.
* `tableName` - (Required) Specifies the AWS Glue table that contains the column information that constitutes your data schema.
* `catalogId` - (Optional) The ID of the AWS Glue Data Catalog. If you don't supply this, the AWS account ID is used by default.
* `region` - (Optional) If you don't specify an AWS Region, the default is the current region.
* `versionId` - (Optional) Specifies the table version for the output data schema. Defaults to `latest`.

#### dynamic_partitioning_configuration

Required when using [dynamic partitioning](https://docs.aws.amazon.com/firehose/latest/dev/dynamic-partitioning.html).

* `enabled` - (Optional) Enables or disables dynamic partitioning. Defaults to `false`.
* `retryDuration` - (Optional) Total amount of seconds Firehose spends on retries. Valid values between 0 and 7200. Default is 300.

~> **NOTE:** You can enable dynamic partitioning only when you create a new delivery stream. Once you enable dynamic partitioning on a delivery stream, it cannot be disabled on this delivery stream. Therefore, Terraform will recreate the resource whenever dynamic partitioning is enabled or disabled.

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

* `arn` - The Amazon Resource Name (ARN) specifying the Stream
* `tagsAll` - A map of tags assigned to the resource, including those inherited from the provider [`defaultTags` configuration block](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#default_tags-configuration-block).

[1]: https://aws.amazon.com/documentation/firehose/

## Timeouts

[Configuration options](https://developer.hashicorp.com/terraform/language/resources/syntax#operation-timeouts):

* `create` - (Default `30M`)
* `update` - (Default `10M`)
* `delete` - (Default `30M`)

## Import

Kinesis Firehose Delivery streams can be imported using the stream ARN, e.g.,

```
$ terraform import aws_kinesis_firehose_delivery_stream.foo arn:aws:firehose:us-east-1:XXX:deliverystream/example
```

Note: Import does not work for stream destination `s3`. Consider using `extendedS3` since `s3` destination is deprecated.

<!-- cache-key: cdktf-0.17.0-pre.15 input-37e4c973386136e67d31c77f5564e1c25fde1d078a2b3cc055984e137d0e641e -->